import java_cup.runtime.*;
import java_cup.*;
import java.io.File;
import java.io.FileInputStream;
import java.io.InputStream;
import java.io.InputStreamReader;
import java.util.List;
import java.util.ArrayList;

import ast.*;

action code
{:
  ast.Statechart statechart = null;
:}
parser code 
{:
  public void syntax_error(Symbol s){
    System.out.println("compiler has detected a syntax error at line " + s.left 
      + " column " + s.right+"Symbol <"+s.toString()+">");
  }
:}

/*
non terminal event_definition,type_specifier,identifier_list,assignment_statement,arithmetic_expr,event_block,event_statement_list,event_dest_state,event_statement,guard_expression,action_statement,message_statement;
non terminal namespace_identifier,string_type,number_type,user_type,expr_LHS,expr_RHS;
non terminal cnfdnf_formula,relational_expr,logical_operator,relational_operator,trans_flow,trans_stmts;
*/

non terminal ast.Statechart       statechart;
non terminal ast.State            state;
non terminal ast.Transition       transition;
non terminal ast.Name             name;
non terminal List<ast.State>      states;
non terminal List<ast.Transition> transitions;

non terminal ast.StatementList       statement_list;
non terminal ast.Statement           statement;
non terminal ast.AssignmentStatement assignment_statement;
non terminal ast.Expression          expr;

non terminal List<ast.Declaration> decl_list;
non terminal ast.Declaration     decl;

terminal String IDENTIFIER;
terminal Symbol
    LBRACE, RBRACE, LPAR, RPAR, LPAR_SQ,RPAR_SQ,     /* Brackets                 */
    SEMICOLON, COLON, COMMA, DOT,                    /* Punctuations             */ 
    ASSIGN, OP_MESSAGE,
    GOTO, DONE,                                      /* Control flow             */
    STATECHART, STATE, TRANSITION,                   /* Statechart related       */
    SRC, DEST, GUARD, ACTION,                        /* Transition label related */
    EMPTY, INIT, CLICK, LIST, DEFAULT, TYPE, EVENT,  /* Event related            */
    BELONGSTO,                                       /* Set theoretic            */
    VAR, IVAR, DVAR, MVAR, CONSTANT, STRING, NUMBER, /* Variables related        */
    AND, OR, NOT,                                    /* Logical operators        */
    INC, DEC,                                        /* Increment, decrement     */
    EQU, NEQ, GRE, GEQ, LES, LEQ,                    /* Relational operators     */
    MIN, ADD, MUL, DIV, UMIN, MOD;                   /* Arithmetic operators     */

precedence nonassoc LES, LEQ, EQU, NEQ, GRE, GEQ;
precedence left LBRACE, RBRACE, AND, OR;
precedence right DOT;

start with statechart;

/*********** fully qualified names like s1.s11 ****************/
name ::=
    IDENTIFIER:id {:
      RESULT = new ast.Name(id);
    :}
  |  name:idlist DOT IDENTIFIER:id
    {:
      RESULT = idlist;
      RESULT.add(id);
    :}
  ;

/*********** skeletal statechart structure - begin *******/
statechart ::= 
    STATECHART IDENTIFIER:id LBRACE
      decl_list:dlist
      states:st transitions:tr
    RBRACE
    {:
      RESULT = new ast.Statechart(id, st, tr, dlist);
    :}
  ;

state ::= 
    STATE IDENTIFIER:id LBRACE
      decl_list:dlist
      states:st transitions:tr
    RBRACE
    {:
      RESULT = new ast.State(id, st, tr, dlist);
    :}
  ;

states ::=
    states:slist state:s 
    {:
      RESULT = slist;
      RESULT.add(s);
    :}
  | {:
       RESULT = new ArrayList<ast.State>();
    :}
  ;

transition ::=
    TRANSITION IDENTIFIER:id LBRACE
      SRC  COLON name:src SEMICOLON
      DEST COLON name:des SEMICOLON
      GUARD COLON expr:g SEMICOLON
      ACTION COLON statement:a
    RBRACE
    {:
      RESULT = new ast.Transition(id, src, des, g, a);
    :}
  ;

transitions ::=
    transitions:tlist transition:t 
    {:
      if(tlist != null) {
        RESULT = tlist;
      }
      else {
        RESULT = new ArrayList<ast.Transition>();
      }
      RESULT.add(t);
    :}
  | {:
      RESULT = null;
    :}
  ;
/*********** skeletal statechart structure - end *******/

/********** expressions for action language - begin ******/
expr ::=
    name:n {:
      RESULT = n;
    :}
  ;

statement_list::=
    statement_list:sl statement:s 
    {:
      sl.add(s);
      RESULT = sl;
    :}
  |
    {:
      RESULT = new ast.StatementList();
    :}
  ;

statement::=
    expr SEMICOLON
  | name:n ASSIGN expr:e SEMICOLON
    {:
      RESULT = new ast.AssignmentStatement(n, e);
    :}
  | LBRACE
      statement_list:sl
    RBRACE
    {:
      RESULT = sl;
    :}
  ;

decl_list ::= decl_list:dlist decl:d
    {:
       if(dlist != null) {
        RESULT = dlist;
      }
      else {
        RESULT = new ArrayList<ast.Declaration>();
      }
      RESULT.add(d);
    :}
  | {:
      RESULT = null;
    :}
  ;

decl ::=
     IDENTIFIER:id COLON IDENTIFIER:tid SEMICOLON
    {:
      RESULT = new ast.Declaration(id, tid);
    :}
  ;

/********** expressions for action language - end ******/

/*
statement_list::=
	statement SEMICOLON
	| statement_list statement SEMICOLON
	| statement_list statement
	;

statement::=
	declaration_statement
	| COLON state_flowcontent
	| init_KEYWORD COLON empty_KEYWORD {: System.out.println ("Empty Init statement is seen"); :}
	| init_KEYWORD COLON assignment_statement {: System.out.println ("Init statement + assignment stmt is seen"); :}
	| default_KEYWORD COLON IDENTIFIER:id {: System.out.println ("Default state is "+id); :}
	;

declaration_statement::=
	IDENTIFIER:id COLON IDENTIFIER:tid SEMICOLON {}
	identifier_list COLON IDENTIFIER:id COMMA declaration_statement {: System.out.println ("Declaration statement is seen " + id); :}
	| identifier_list COLON IDENTIFIER:id {: System.out.println ("Declaration statement is seen of User type"); :}
	| identifier_list COLON STRING_KEYWORD {: System.out.println ("Declaration statement is seen of String type"); :}
	| identifier_list COLON IDENTIFIER:id1 LIST_KEYWORD COMMA declaration_statement {: System.out.println ("List of User type: "+id1); :}
	| identifier_list COLON IDENTIFIER:id1 LIST_KEYWORD {: System.out.println ("List of User type: "+id1); :}
	;
assignment_statement::= 
	expr_LHS OP_ASSIGN expr_RHS {: System.out.println ("Assignment statement is seen"); :}
	| expr_LHS OP_ASSIGN expr_RHS COMMA assignment_statement {: System.out.println ("Assignment statements is seen"); :}
	;

identifier_list::=
	IDENTIFIER:id {: System.out.println ("Identifier : "+id); :}
	|identifier_list COMMA IDENTIFIER:id {: System.out.println ("Identifier List : "+id); :}
	;


expr_RHS::=expr_LHS|arithmetic_expr|CONSTANT;
event_definition::=
	IDENTIFIER:id OP_DOT CLICK_KEYWORD LPAR_SQ cnfdnf_formula RPAR_SQ action_statement DONE_KEYWORD SEMICOLON {: System.out.println ("Event Declaration is seen : "+id); :} trans_stmts 
	| IDENTIFIER:id OP_DOT CLICK_KEYWORD LPAR_SQ RPAR_SQ action_statement DONE_KEYWORD SEMICOLON {: System.out.println ("Event Declaration is seen : "+id); :} trans_stmts 
	| IDENTIFIER:id OP_DOT CLICK_KEYWORD LPAR_SQ RPAR_SQ DONE_KEYWORD SEMICOLON {: System.out.println ("Event Declaration is seen : "+id); :} trans_stmts 
	| IDENTIFIER:id OP_DOT CLICK_KEYWORD LPAR_SQ cnfdnf_formula RPAR_SQ action_statement DONE_KEYWORD SEMICOLON {: System.out.println ("Event Declaration is seen : "+id); :}
	| IDENTIFIER:id OP_DOT CLICK_KEYWORD LPAR_SQ RPAR_SQ action_statement DONE_KEYWORD SEMICOLON {: System.out.println ("Event Declaration is seen : "+id); :}
	| IDENTIFIER:id OP_DOT CLICK_KEYWORD LPAR_SQ RPAR_SQ DONE_KEYWORD SEMICOLON {: System.out.println ("Event Declaration is seen : "+id); :} 

	;
cnfdnf_formula::= 
		relational_expr
		|relational_expr logical_operator cnfdnf_formula
		| namespace_identifier logical_operator cnfdnf_formula
		| namespace_identifier		
		;

relational_expr::=expr_RHS relational_operator expr_RHS;
logical_operator::=AND|OR;
relational_operator::=LES|LEQ|EQU|NEQ|GRE|GEQ|belongs_to_KEYWORD;
expr_LHS::=IDENTIFIER|namespace_identifier;
namespace_identifier::=IDENTIFIER:id OP_DOT IDENTIFIER:id2 {: System.out.println ("Name Space Identifier "+id +" " + id2); :}
		| namespace_identifier OP_DOT IDENTIFIER:id1 {: System.out.println ("Name Space Identifier "+id1); :}
		;
action_statement::= 
	assignment_statement {: System.out.println ("action statement is seen"); :}
	;

string_type::=IDENTIFIER:id COLON STRING_KEYWORD {: System.out.println ("String type: "+id); :} 
	;
number_type::=IDENTIFIER:id COLON NUMBER_KEYWORD {: System.out.println ("Number type: "+id); :} 
	;
user_type::=IDENTIFIER:id COLON IDENTIFIER:id1 LIST_KEYWORD {: System.out.println ("List of User type: "+id + "Type Name: " +id1); :} 
	| IDENTIFIER:id COLON IDENTIFIER:id1 {: System.out.println ("User type: "+id + "Type Name: " +id1); :} 
	;

*/
